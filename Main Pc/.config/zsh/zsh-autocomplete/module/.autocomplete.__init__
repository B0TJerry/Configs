#!/bin/zsh
zmodload -Fa zsh/files b:zf_mkdir b:zf_rm
zmodload -Fa zsh/parameter p:functions
zmodload -Fa zsh/system p:sysparams
zmodload -Fa zsh/zleparameter p:widgets
zmodload -Fa zsh/zutil b:zstyle
builtin autoload -Uz add-zsh-hook zmathfunc
zmathfunc

.autocomplete.__init__.precmd() {
  emulate -L zsh -o NO_aliases
  setopt $_autocomplete__options

  # Workaround for https://github.com/zdharma/zinit/issues/366
  [[ -v functions[.zinit-shade-off] ]] &&
    .zinit-shade-off "${___mode:-load}"

  local 0=${(%):-%N}
  add-zsh-hook -d precmd $0
  unfunction $0

  # Workaround for https://www.zsh.org/mla/workers/2021/msg01310.html
  if zstyle -L zle-hook types > /dev/null; then
    local hook
    for hook in isearch-{exit,update} line-{pre-redraw,init,finish} history-line-set keymap-select
    do
      [[ $widgets[zle-$hook] == user:_zsh_highlight_widget_orig-s*-r<->-zle-$hook ]] &&
          zle -N zle-$hook azhw:zle-$hook
    done
  fi

  # Don't let `&` and `|` remove suffixes.
  [[ -v ZLE_REMOVE_SUFFIX_CHARS ]] ||
    typeset -g ZLE_REMOVE_SUFFIX_CHARS=$' \t\n;'
  [[ -v ZLE_SPACE_SUFFIX_CHARS ]] ||
    typeset -g ZLE_SPACE_SUFFIX_CHARS=$'&|'

  local mod; for mod in compinit config widget key key-binding recent-dirs async; do
    mod=.autocomplete.$mod.precmd
    if [[ -v functions[$mod] ]]; then
      $mod
      unfunction $mod
    fi
  done
}

local xdg_data_home=${XDG_DATA_HOME:-$HOME/.local/share}

local zsh_data_dir=$xdg_data_home/zsh
[[ -d $zsh_data_dir ]] ||
    zf_mkdir -pm 0700 $zsh_data_dir

local logdir=$xdg_data_home/zsh-autocomplete/log
[[ -d $logdir ]] ||
    zf_mkdir -p -- $logdir
local -a older_than_a_week=( $logdir/*(Nmd+7) )
(( $#older_than_a_week[@] )) &&
    zf_rm -f -- $older_than_a_week[@]
typeset -gH _autocomplete__log_file=$logdir/${(%):-%D{%F}}.log

typeset -gH _autocomplete__prev=$'\0' _autocomplete__next=$'\n# %e. %N'
typeset -gH _autocomplete__time=$' [%D{%T.%.}]\n' _autocomplete__ps4=$'%(?,,\t-> %?\n)'
_autocomplete__ps4+='${${_autocomplete__prev:#${(%)_autocomplete__next}}:+'
_autocomplete__ps4+='${_autocomplete__prev::=${(%)_autocomplete__next}}${(%)_autocomplete__time}}'
_autocomplete__ps4+=$'%I\t%(1_,%_ ,)'

local zsh_cache_dir=${XDG_CACHE_HOME:-$HOME/.cache}/zsh
[[ -d $zsh_cache_dir ]] ||
    zf_mkdir -pm 0700 $zsh_cache_dir

local mod; for mod in compinit config widget key key-binding recent-dirs async; do
  if zstyle -T ':autocomplete:' $mod; then
    source ~zsh-autocomplete/module/.autocomplete.$mod
  fi
done

add-zsh-hook precmd .autocomplete.__init__.precmd
