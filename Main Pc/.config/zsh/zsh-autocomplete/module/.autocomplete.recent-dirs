#!/bin/zsh
zmodload -Fa zsh/files b:zf_mv
zmodload -Fa zsh/parameter p:commands p:dirstack p:functions

.autocomplete.recent-dirs.precmd() {

  if [[ -v functions[zshz] && -v functions[_zshz_precmd] ]] &&
      zstyle -T ':autocomplete:' recent-dirs 'zsh-z'; then
    _autocomplete.recent_dirs() {
      reply=( ${(f)"$( zshz --complete -l $1 2> /dev/null )"} )
    }

  elif [[ -v commands[zoxide] && -v functions[__zoxide_hook] ]] &&
      zstyle -T ':autocomplete:' recent-dirs 'zoxide'; then
    _autocomplete.recent_dirs() {
      reply=( ${(f)"$( zoxide query --list $1 2> /dev/null )"} )
    }

  elif [[ -v functions[_zlua] && -v functions[_zlua_precmd] ]] &&
      zstyle -T ':autocomplete:' recent-dirs 'z.lua'; then
    _autocomplete.recent_dirs() {
      reply=( ${${(f)"$( _zlua --complete $1 2> /dev/null )"}##<->[[:space:]]##} )
    }

  elif [[ -v functions[_z] && -v functions[_z_precmd] ]] &&
      zstyle -T ':autocomplete:' recent-dirs 'z.sh'; then
    _autocomplete.recent_dirs() {
      reply=( ${${(fOa)"$( _z -l $1 2>&1 )"}##(common:|<->)[[:space:]]##} )
    }

  elif [[ -v commands[autojump] && -v AUTOJUMP_SOURCED ]] &&
      zstyle -T ':autocomplete:' recent-dirs 'autojump'; then
    _autocomplete.recent_dirs() {
      reply=( ${${(f)"$( autojump --complete $1 2> /dev/null )"}##${1}__<->__} )
    }

  elif [[ ( -v commands[fasd] || -v functions[fasd] ) && -v functions[_fasd_preexec] ]] &&
      zstyle -T ':autocomplete:' recent-dirs 'fasd'; then

    _autocomplete.recent_dirs() {
      reply=( ${(f)"$( fasd -dlR $1 2> /dev/null )"} )
    }

  elif builtin autoload -Uz chpwd_recent_dirs chpwd_recent_filehandler &&
      zstyle -T ':autocomplete:' recent-dirs 'cdr'; then
    setopt autopushd pushdignoredups

    if ! zstyle -s :chpwd: recent-dirs-file _; then
      local old=${ZDOTDIR:-$HOME}/.chpwd-recent-dirs
      local new=${XDG_DATA_HOME:-$HOME/.local/share}/zsh/chpwd-recent-dirs
      zstyle ':chpwd:*' recent-dirs-file $new
      [[ -e $old && ! -e $new ]] &&
          zf_mv "$old" "$new"
    fi
    zstyle -s :chpwd: recent-dirs-max _ ||
        zstyle ':chpwd:*' recent-dirs-max 0

    if ! (( $#dirstack[@] )); then
      local -aU reply=()
      chpwd_recent_filehandler
      dirstack=( ${^reply[@]:#$PWD}(N-/) )
    fi

    _autocomplete.recent_dirs.save() {
      chpwd_recent_filehandler $PWD $dirstack[@]
    }
    add-zsh-hook chpwd _autocomplete.recent_dirs.save

    _autocomplete.recent_dirs() {
      reply=( $^dirstack[@](N) )

      local ancestor=$PWD:h
      while [[ $ancestor != / ]]; do
        reply=( ${reply[@]:#$ancestor} )
        ancestor=$ancestor:h
      done
      _autocomplete.sort_by_length ${(D)reply[@]:#$PWD(|/[^/]#)}
      reply=( $~reply[@] )

      (( $#reply[@] ))
    }
  fi

  if [[ ( -v commands[fasd] || -v functions[fasd] ) && -v functions[_fasd_preexec] ]] &&
      zstyle -T ':autocomplete:' recent-files 'fasd'; then

    _autocomplete.recent_files() {
      reply=( $( fasd -flR $1 2> /dev/null ) )
    }
  fi
}
