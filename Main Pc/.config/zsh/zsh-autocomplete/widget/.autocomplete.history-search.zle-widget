#!/bin/zsh
local 0=${(%):-%N}
zle -C ${0}.context list-choices ${0}.context

${0}.context() {
  lcontext=${LBUFFER%${(~j:*:)words[1,CURRENT-1]}*$QIPREFIX$IPREFIX$PREFIX}
  lcontext+=${lcontext:+ }${(pj:\0:)words}
  rcontext=${RBUFFER#$SUFFIX$ISUFFIX$QISUFFIX*${(~j:*:)words[CURRENT+1,-1]}}
}

${0}() {
  local 0=${(%):-%N}
  setopt localoptions completeinword

  local lbuffer="$LBUFFER" rbuffer="$RBUFFER"
  local keymap_menuselect="$( bindkey -M menuselect -L )"
  {

    bindkey -M menuselect -s '^R' '^_^_^R'
    bindkey -M menuselect -s '^S' '^_^_^S'
    bindkey -M menuselect $key[Left] .backward-char
    bindkey -M menuselect $key[Right] .forward-char

    local lcontext rcontext
    zle ${0}.context
    LBUFFER="$lcontext"
    RBUFFER="$rcontext"

    [[ -o sharehistory ]] &&
        fc -RI  # Get new history events from file.
    zle _history_search

  } always {
    if [[ $BUFFER == $lcontext$rcontext ]] then
      LBUFFER="$lbuffer"
      RBUFFER="$rbuffer"
    fi
    bindkey -M menuselect -r '^R'
    bindkey -M menuselect -r '^S'
    bindkey -M menuselect -r $key[Left]
    bindkey -M menuselect -r $key[Right]
    eval "$keymap_menuselect"
  }
}

$0 "$@"
